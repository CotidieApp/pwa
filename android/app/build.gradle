apply plugin: 'com.android.application'
import org.gradle.api.file.DuplicatesStrategy

android {
    namespace = "com.benjamin.studio"
    compileSdk = rootProject.ext.compileSdkVersion
    
    signingConfigs {
        release {
            storeFile file("my-release-key.keystore")
            storePassword "Qwog2bfoa3"
            keyAlias "my-key-alias"
            keyPassword "Qwog2bfoa3"
        }
    }

    defaultConfig {
        applicationId "com.benjamin.studio"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 28
        versionName "4.3.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern = '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation "androidx.work:work-runtime:2.10.0"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

tasks.register('copySaintsDataToAssets', Copy) {
    from("${rootProject.projectDir}/../src/lib/saints-data.json")
    into("${project.projectDir}/src/main/assets")
    rename { 'saints-data.json' }
}

tasks.register('copyNotificationIconsToDrawable', Copy) {
    from("${rootProject.projectDir}/../public/icons") {
        include 'small_icon_black.png'
        include 'small_icon_white.png'
    }
    into("${project.projectDir}/src/main/res/drawable")
}

def toDrawableResourceName = { String relativePath ->
    def normalized = relativePath.replace('\\', '/').toLowerCase()
    def ext = ''
    if (normalized.lastIndexOf('.') > normalized.lastIndexOf('/')) {
        ext = normalized.substring(normalized.lastIndexOf('.') + 1)
        normalized = normalized.substring(0, normalized.lastIndexOf('.'))
    }
    def sanitized = normalized.replaceAll(/[^a-z0-9_]/, '_')
    sanitized = sanitized.replaceAll(/_+/, '_')
    sanitized = sanitized.replaceAll(/^_+|_+$/, '')
    if (!(sanitized ==~ /^[a-z].*/)) {
        sanitized = "img_${sanitized}"
    }
    if (ext) {
        sanitized = "${sanitized}_${ext}"
    }
    return sanitized
}

tasks.register('copyNotificationImagesToDrawable', Copy) {
    from("${rootProject.projectDir}/../public") {
        include 'images/**/*.png'
        include 'images/**/*.jpg'
        include 'images/**/*.jpeg'
        include 'images/**/*.webp'
        include 'icons/icon.png'
        includeEmptyDirs = false
        eachFile { fileCopyDetails ->
            def path = fileCopyDetails.relativePath.pathString
            def extension = ''
            def dotIdx = path.lastIndexOf('.')
            if (dotIdx >= 0) {
                extension = path.substring(dotIdx)
            }
            def renamed = toDrawableResourceName(path)
            fileCopyDetails.path = "${renamed}${extension}"
        }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    into("${project.projectDir}/src/main/res/drawable")
}

tasks.register('copyImageDisplayPreferencesToAssets', Copy) {
    from("${rootProject.projectDir}/../src/lib/image-display.ts")
    into("${project.projectDir}/src/main/assets")
}

preBuild.dependsOn(tasks.named('copySaintsDataToAssets'))
preBuild.dependsOn(tasks.named('copyNotificationIconsToDrawable'))
preBuild.dependsOn(tasks.named('copyNotificationImagesToDrawable'))
preBuild.dependsOn(tasks.named('copyImageDisplayPreferencesToAssets'))

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
